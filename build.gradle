allprojects {
    group = project.maven_group
    version = project.maven_version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name 'altarik'
                url 'https://repo.altarik.fr/'.concat(project.version.endsWith('SNAPSHOT') ? 'snapshots/' : 'releases/')
                credentials {
                    username = project.repo_username
                    password = project.repo_password
                }
            }
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_version}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junit_version}"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        maven {
            name 'altarik-snapshots'
            url 'https://repo.altarik.fr/snapshots/'
        }
        maven {
            name 'altarik-releases'
            url 'https://repo.altarik.fr/releases/'
        }

        mavenCentral()
    }
}
